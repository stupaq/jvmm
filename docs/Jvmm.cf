-- JVMM : BNF syntax specification --
-------------------------------------

entrypoints Program;

-- DECLARATION --
-----------------

DVariable.    Declaration ::= Type Ident;

-- PROGRAM --
--------------

Program.      Program ::= [Definition];

DFunction.    Definition ::= Function;

DClass.       Definition ::= Class;

separator nonempty Definition "";

-- FUNCTION --
--------------

Function.     Function ::= Type Ident "(" [Argument] ")" Exceptions "{" [Stmt] "}";

Argument.     Argument ::= Type Ident;

separator Argument ",";

NoExceptions. Exceptions ::= ;

Exceptions.   Exceptions ::= "throws" [Type];

-- CLASS --
-----------

Class.        Class ::= "class" Ident Extends "{" [Member] "}";

Field.        Member ::= Declaration ";";

Method.       Member ::= Function;

terminator nonempty Member "";

SuperClass.   Extends ::= "extends" Type;

SuperObject.  Extends ::= ;

-- TYPES --
-----------

-- EXTENSIONS/
TObject.    Type ::= "Object";

TUser.      Type ::= Ident;

TArray.     Type ::= Type "[]";

TChar.      Type ::= "char";
-- /EXTENSIONS

TInt.       Type ::= "int";

TString.    Type ::= "string";

TBool.      Type ::= "boolean";

TVoid.      Type ::= "void";

separator nonempty Type ",";

-- STATEMENTS --
----------------

-- EXTENSIONS/
SThrow.         Stmt ::= "throw" Expr ";";

STryCatch.      Stmt ::= "try" Stmt "catch" "(" Type Ident ")" Stmt;
-- /EXTENSIONS

SBlock.         Stmt ::= "{" [Stmt] "}";

SEmpty.         Stmt ::= ";";

SDeclVar.       Stmt ::= Type [Item] ";";

separator Stmt "";

NoInit.         Item ::= Ident;

Init.           Item ::= Ident "=" Expr;

separator nonempty Item ",";

SAssign.        Stmt ::= Ident "=" Expr  ";";

-- EXTENSIONS/
SAssignArr.     Stmt ::= Ident "[" Expr "]" "=" Expr  ";";

SAssignFld.     Stmt ::= Ident "." Ident "=" Expr  ";";

SAssignThis.    Stmt ::= "self" "." Ident "=" Expr  ";";
-- /EXTENSIONS

SPostInc.       Stmt ::= Ident "++" ";";

SPostDec.       Stmt ::= Ident "--" ";";

SAssignOp.      Stmt ::= Ident AssignOp Expr  ";";

-- EXTENSIONS/
SAssignOpArr.   Stmt ::= Ident "[" Expr "]" AssignOp Expr  ";";

SAssignOpFld.   Stmt ::= Ident "." Ident AssignOp Expr  ";";

SAssignOpThis.  Stmt ::= "self" "." Ident AssignOp Expr  ";";
-- /EXTENSIONS

SReturn.        Stmt ::= "return" Expr ";";

SReturnV.       Stmt ::= "return" ";";

-- The following two rules are responsible for the only shift/reduce conflict,
-- tested in conflict001 testcase.
SIf.            Stmt ::= "if" "(" Expr ")" Stmt;

SIfElse.        Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt;

SWhile.         Stmt ::= "while" "(" Expr ")" Stmt;

SForeach.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt;

SExpr.          Stmt ::= Expr ";";

-- EXPRESSIONS --
-----------------

-- EXTENTIONS/
EArrayE.    Expr6 ::= Expr7 "[" Expr "]";

EMethodE.   Expr6 ::= Expr7 "." Ident "(" [Expr] ")";

EFieldE.    Expr6 ::= Expr7 "." Ident;

EArrayI.    Expr6 ::= Ident "[" Expr "]";

EMethodI.   Expr6 ::= Ident "." Ident "(" [Expr] ")";

EFieldI.    Expr6 ::= Ident "." Ident;

ENewObject. Expr6 ::= "new" Type;

ENewArray.  Expr6 ::= "new" Type "[" Expr "]";

EMethodIT.  Expr6 ::= "self" "." Ident "(" [Expr] ")";

EFieldIT.   Expr6 ::= "self" "." Ident;

EThis.      Expr6 ::= "self";

ENullT.     Expr6 ::= "(" Type ")null";

ENull.      Expr6 ::= "null";

ELitChar.   Expr6 ::= Char;
-- /EXTENSIONS

EVar.       Expr6 ::= Ident;

ELitInt.    Expr6 ::= Integer;

ELitTrue.   Expr6 ::= "true";

ELitFalse.  Expr6 ::= "false";

EApp.       Expr6 ::= Ident "(" [Expr] ")";

EString.    Expr6 ::= String;

-- Original grammary says that `Expr5 ::= "-" Expr6`, this rule prohibits many not-so-useful
-- expressions like `- -1`. Being a total fan of weird stuff, I have changed that.
ENeg.       Expr5 ::= "-" Expr5;

-- See comment on previous rule.
ENot.       Expr5 ::= "!" Expr5;

EMul.       Expr4 ::= Expr4 MulOp Expr5;

EAdd.       Expr3 ::= Expr3 AddOp Expr4;

ERel.       Expr2 ::= Expr2 RelOp Expr3;

EAnd.       Expr1 ::= Expr1 "&&" Expr2;

EOr.        Expr ::= Expr "||" Expr1;

coercions Expr 7;

separator Expr ",";

-- OPERATORS --
---------------

APlus.      AssignOp ::= "+=";

AMinus.     AssignOp ::= "-=";

ATimes.     AssignOp ::= "*=";

ADiv.       AssignOp ::= "/=";

AMod.       AssignOp ::= "%=";

Plus.       AddOp ::= "+";

Minus.      AddOp ::= "-";

Times.      MulOp ::= "*";

Div.        MulOp ::= "/";

Mod.        MulOp ::= "%";

LTH.        RelOp ::= "<";

LEQ.        RelOp ::= "<=";

GTH.        RelOp ::= ">";

GEQ.        RelOp ::= ">=";

EQU.        RelOp ::= "==";

NEQ.        RelOp ::= "!=";

-- TOKENS --
------------

-- COMMENTS --
--------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

