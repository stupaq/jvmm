-- JVMM : BNF syntax specification --
-------------------------------------

entrypoints Program;

-- DECLARATION --
-----------------
DVariable.    Declaration ::= Type Ident;

-- PROGRAM --
--------------
Program.      Program ::= [Definition];
separator nonempty Definition "";

DFunction.    Definition ::= Function;
DClass.       Definition ::= Class;

-- FUNCTION --
--------------
Function.     Function ::= Type Ident "(" [Argument] ")" Exceptions "{" [Stmt] "}";
separator Argument ",";

NoExceptions. Exceptions ::= ;
Exceptions.   Exceptions ::= "throws" [Type];
separator nonempty Type ",";

Argument.     Argument ::= Type Ident;

-- CLASS --
-----------
Class.        Class ::= "class" Ident Extends "{" [Member] "}";
terminator nonempty Member "";

Field.      Member ::= Declaration ";";
Method.     Member ::= Function;

SuperClass.   Extends ::= "extends" Type;
SuperObject.  Extends ::= ;

-- TYPES --
-----------

TVoid.      Type ::= "void";
TInt.       Type ::= "int";
TChar.      Type ::= "char";
TBool.      Type ::= "boolean";

TString.    Type ::= "String"; -- for compatibility with Java
TString.    Type ::= "string";
TObject.    Type ::= "Object";
TUser.      Type ::= Ident;

TArray.     Type ::= Type "[]";


-- TODO --

-- STATEMENTS --
----------------
SBlock.         Stmt ::= "{" [Stmt] "}";
separator Stmt "";

SAssignOp.      Stmt ::= Ident AssignOp Expr  ";";
SAssignOpArr.   Stmt ::= Ident "[" Expr "]" AssignOp Expr  ";";
SAssignOpFld.   Stmt ::= Ident "." Ident AssignOp Expr  ";";
SAssignOpThis.  Stmt ::= "self" "." Ident AssignOp Expr  ";";

SPostInc.       Stmt ::= Ident "++" ";";
SPostDec.       Stmt ::= Ident "--" ";";

SEmpty.         Stmt ::= ";";

SAssign.        Stmt ::= Ident "=" Expr  ";";
SAssignArr.     Stmt ::= Ident "[" Expr "]" "=" Expr  ";";
SAssignFld.     Stmt ::= Ident "." Ident "=" Expr  ";";
SAssignThis.    Stmt ::= "self" "." Ident "=" Expr  ";";

SReturn.        Stmt ::= "return" Expr ";";
SReturnV.       Stmt ::= "return" ";";

-- The following two rules are responsible for the only shift/reduce conflict,
-- tested in examples_good/conflict001.jv.
SIf.            Stmt ::= "if" "(" Expr ")" Stmt;
SIfElse.        Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt;

SWhile.         Stmt ::= "while" "(" Expr ")" Stmt;
SForeach.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt;

SExpr.          Stmt ::= Expr ";";

SThrow.         Stmt ::= "throw" Expr ";";
STryCatch.      Stmt ::= "try" Stmt "catch" "(" Type Ident ")" Stmt;

SDeclVar.       Stmt ::= Type [Item] ";";
separator nonempty Item ",";

NoInit.         Item ::= Ident;
Init.           Item ::= Ident "=" Expr;

-- EXPRESSIONS --
-----------------

-- EXTENTIONS/
EArray.     Expr6 ::= Expr7 "[" Expr "]";

EMethod.    Expr6 ::= Expr7 "." Ident "(" [Expr] ")";

EField.     Expr6 ::= Expr7 "." Ident;

EArrayI.    Expr6 ::= Ident "[" Expr "]";

EMethodI.   Expr6 ::= Ident "." Ident "(" [Expr] ")";

EFieldI.    Expr6 ::= Ident "." Ident;

ENewObject. Expr6 ::= "new" Type;

ENewArray.  Expr6 ::= "new" Type "[" Expr "]";

EThis.      Expr6 ::= "self";

ENullT.     Expr6 ::= "(" Type ")null";

ENull.      Expr6 ::= "null";

ELitChar.   Expr6 ::= Char;
-- /EXTENSIONS

EVar.       Expr6 ::= Ident;

ELitInt.    Expr6 ::= Integer;

ELitTrue.   Expr6 ::= "true";

ELitFalse.  Expr6 ::= "false";

EApp.       Expr6 ::= Ident "(" [Expr] ")";

EString.    Expr6 ::= String;

ENeg.       Expr5 ::= "-" Expr6;

ENot.       Expr5 ::= "!" Expr6;

EMul.       Expr4 ::= Expr4 MulOp Expr5;

EAdd.       Expr3 ::= Expr3 AddOp Expr4;

ERel.       Expr2 ::= Expr2 RelOp Expr3;

EAnd.       Expr1 ::= Expr1 "&&" Expr2;

EOr.        Expr ::= Expr "||" Expr1;

coercions Expr 7;

separator Expr ",";

-- OPERATORS --
---------------

-- FIXME remove these
APlus.      AssignOp ::= "+=";
AMinus.     AssignOp ::= "-=";
ATimes.     AssignOp ::= "*=";
ADiv.       AssignOp ::= "/=";
AMod.       AssignOp ::= "%=";

Plus.       AddOp ::= "+";
Minus.      AddOp ::= "-";

Times.      MulOp ::= "*";
Div.        MulOp ::= "/";
Mod.        MulOp ::= "%";

LTH.        RelOp ::= "<";
LEQ.        RelOp ::= "<=";
GTH.        RelOp ::= ">";
GEQ.        RelOp ::= ">=";
EQU.        RelOp ::= "==";
NEQ.        RelOp ::= "!=";

-- TOKENS --
------------

-- COMMENTS --
--------------

comment    "#" ;
comment    "//" ;
comment    "/*" "*/" ;
